#!/bin/tcsh -f

# paths
set pkg=/usr/sbin/slackpkg
set plst=/var/lib/slackpkg/PACKAGES.TXT

# me
set me = $0
set mes = $me:t
set mever = "1.1"

# args
set todo = help
if ( $#argv > 0 ) then
	set todo = $argv[1]
	shift
endif

#
switch ( $todo )
case h:
case -h:
case help:
case -help:
case --help:
	echo "$mes version $mever, the Nicolas Slackware Package Utilities."
	echo "Copyright Â© 2016, Nicholas D. Christopoulos <mailto:nereus@freemail.gr>."
	echo "License GPLv3+: GNU GPL version 3 or later <http://gnu.org/licenses/gpl.html>."
	echo " "
	echo "usage: $mes option [package|file]"
	echo "where option:"
	echo "	h,help			help"
	echo "	u,update		update + upgrade-all + install-new"
	echo "	s,search		search for package"
	echo "	i,install		install package"
	echo "	ri,reinstall		reinstall package"
	echo "	r,remove		remove package"
	echo "	f,file-search		search for file in packages"
	echo "	v,view,info		display information about package"
	echo "	p,package-list		print package list with description"
	echo "	spl			search package list"
	echo "  l,list		list installed packages"
	echo "	sil			search installed list"
	echo " "
	echo "	help-fix		fix-scripts"
	echo " "
	echo "If you need more information try to use '$mes help', 'slackpkg help' or look the slackpkg's manpage."
	breaksw

case help-fix:
	echo "	fix-greek	fixs greek language sets at /etc/profile.d"
	echo "	non-greek-fonts	display all fonts without greek support"
	breaksw

case i:
case install:
	$pkg install $*
	breaksw

case ri:
case re:
case reinstall:
	$pkg reinstall $*
	breaksw

case r:
case remove:
	$pkg remove $*
	breaksw

case u:
case update:
	$pkg update
	$pkg upgrade-all
	$pkg install-new
	breaksw

case s:
case search:
	$pkg search $*
	breaksw

case f:
case file-search:
	$pkg file-search $*
	breaksw

case v:
case view:
case info:
	$pkg info $*
	breaksw

case l:
case list:
	slackyd -l
	breaksw

case sil:
	slackyd -l | grep "$*"
	breaksw

case p:
case package-list:
	cat $plst
	breaksw

case spl:
	cat $plst | grep "$*"
	breaksw
	
case pi:
	ls /var/log/packages/
	breaksw

#
#	FIXES
#

# set LANG=el_GR.*
case fix-greek:
	set tmp = /tmp/lang.tmp
	set lst = ( /etc/profile.d/lang.{csh,sh} )
	foreach f ( $lst )
		sed s/en_US/el_GR/g < $f > $tmp && cat $tmp > $f && echo $f fixed
	end
	rm $tmp
	breaksw

# display all non-greek fonts
case non-greek-fonts:
	set allfonts = (`fc-list | awk -F: '{print $1}'`)
	set greekfonts = (`fc-list :lang=el | awk -F: '{print $1}'`)
	foreach f ( $allfonts )
		set found = 0
		foreach gf ( $greekfonts )
			if ( $f == $gf ) then
				set found = 1
				break
			endif
		end
		if ( $found == 0 ) then
			echo $f
		endif
	end
	breaksw

# check passwd
case check-passwd:
	# print users without password
	awk -F: '{if ($2 == "") print $1 ": no password!"}' </etc/passwd
	# print users WITH shell
	awk -F: '{if ($7 !~ "/bin/(false|sync)" && $7 !~ "/sbin/(nologin|halt|shutdown)") print $1":\t" $7;}' </etc/passwd
	breaksw

endsw
